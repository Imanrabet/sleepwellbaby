apply plugin: 'com.android.application'
apply plugin: 'org.jetbrains.kotlin.android.extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android'
apply plugin: 'io.fabric'

def keystorePropertiesFile = rootProject.file("local.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    signingConfigs {
        PremiumSign {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
        FreemiumSign {
            keyAlias keystoreProperties['fkeyAlias']
            keyPassword keystoreProperties['fkeyPassword']
            storeFile file(keystoreProperties['fstoreFile'])
            storePassword keystoreProperties['fstorePassword']
        }
    }
    androidExtensions {
        experimental = true
    }
    compileSdkVersion 27
    defaultConfig {
        applicationId "com.hanihashemi.sleepwellbaby"
        minSdkVersion 16
        targetSdkVersion 27
        versionCode 4
        versionName "1.0.3"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
        setProperty("archivesBaseName", "SleepWellBaby-$versionName")
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            debuggable false
        }
        debug {
            debuggable true
        }
    }
    flavorDimensions "default"
    productFlavors {
        freemium {
            applicationIdSuffix ".freemium"
            signingConfig signingConfigs.FreemiumSign
        }
        premium {
            signingConfig signingConfigs.PremiumSign
        }
    }
    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }
    configurations.all {
        resolutionStrategy.force 'com.android.support:support-annotations:27.1.1'
        resolutionStrategy.force 'com.android.support:support-v4:27.1.1'
        resolutionStrategy.force 'com.android.support:support-media-compat:27.1.1'
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'com.android.support:appcompat-v7:27.1.1'
    implementation 'com.android.support.constraint:constraint-layout:1.1.0'
    implementation 'com.google.firebase:firebase-core:12.0.1'
    implementation 'com.crashlytics.sdk.android:crashlytics:2.9.2'
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'com.jakewharton.timber:timber:4.6.0'
    implementation 'com.gelitenight.waveview:waveview:1.0.0'

    // unit test
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.mockito:mockito-core:2.18.3'
    testImplementation 'org.robolectric:robolectric:3.8'

    // Leak Canary
//    debugImplementation 'com.squareup.leakcanary:leakcanary-android:1.5.4'
//    releaseImplementation 'com.squareup.leakcanary:leakcanary-android-no-op:1.5.4'

    // dagger
//    implementation 'com.google.dagger:dagger:2.11'
//    implementation 'com.google.dagger:dagger-android:2.11'
//    implementation 'com.google.dagger:dagger-android-support:2.11'
//    kapt 'com.google.dagger:dagger-android-processor:2.11'
//    kapt 'com.google.dagger:dagger-compiler:2.11'

    implementation('com.karumi:dexter:4.2.0') {
        exclude group: 'com.android.support', module: 'appcompat'
        exclude group: 'com.android.support', module: 'support-v4'
        exclude group: 'com.android.support', module: 'support-v7'
        exclude group: 'com.android.support', module: 'design'
    }
    implementation('com.github.Kennyc1012:BottomSheet:2.4.0') {
        exclude group: 'com.android.support', module: 'appcompat'
        exclude group: 'com.android.support', module: 'support-v4'
        exclude group: 'com.android.support', module: 'support-v7'
        exclude group: 'com.android.support', module: 'design'
    }

    // UI Test
    androidTestImplementation 'com.android.support.test:rules:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
}
repositories {
    mavenCentral()
}

apply plugin: 'com.google.gms.google-services'